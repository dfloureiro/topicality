apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.dfl.topicality"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 3
        versionName "1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        android.defaultConfig.vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        sourceSets {
            androidTest.assets.srcDirs +=
                    files("$projectDir/schemas".toString())
        }

        buildConfigField "String", "NEWS_API_KEY", localProperties['newsapikey']
    }
    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            zipAlignEnabled false
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    configurations.all { config ->
        if (config.name.contains('UnitTest')) {
            config.resolutionStrategy.eachDependency { details ->
                if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                    details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.15.0"
    androidTestImplementation "org.mockito:mockito-core:2.15.0"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation("android.arch.persistence.room:testing:1.1.0-alpha2") {
        exclude group: 'com.google.code.gson', module: 'gson'
    }

    //LEAKCANARY
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${LEAKCANARY_VERSION}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${LEAKCANARY_VERSION}"

    //ANDROID
    implementation "com.android.support:cardview-v7:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:design:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:customtabs:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support:preference-v7:${ANDROID_SUPPORT_VERSION}"
    implementation "com.android.support.constraint:constraint-layout:${CONSTRAINT_LAYOUT_VERSION}"

    //RXJAVA 2
    implementation "io.reactivex.rxjava2:rxjava:${RXJAVA2_VERSION}"
    implementation "io.reactivex.rxjava2:rxandroid:${RXANDROID_VERSION}"

    //BUTTERKNIFE
    implementation "com.jakewharton:butterknife:${BUTTERKNIFE_VERSION}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${BUTTERKNIFE_VERSION}"

    //GLIDE
    implementation "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    annotationProcessor "com.github.bumptech.glide:compiler:${GLIDE_VERSION}"

    //ROOM
    implementation "android.arch.persistence.room:runtime:${ROOM_VERSION}"
    implementation "android.arch.persistence.room:rxjava2:${ROOM_VERSION}"
    annotationProcessor "android.arch.persistence.room:compiler:${ROOM_ANNOTATION_VERSION}"
    implementation "android.arch.lifecycle:reactivestreams:${ROOM_REACTIVE_VERSION}"

    //CARD STACK VIEW
    implementation("com.yuyakaido.android:card-stack-view:${CARDSTACK_VERSION}") {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    //API
    implementation "com.github.dfloureiro:news-api-kotlin:${NEWS_API_KOTLIN_VERSION}"

    //FIREBASE
    releaseImplementation "com.google.firebase:firebase-core:${FIREBASE_VERSION}"

    //CRASHLYTICS
    releaseImplementation("com.crashlytics.sdk.android:crashlytics:${CRASHLYTICS_VERSION}") {
        transitive = true
    }
}
apply plugin: 'com.google.gms.google-services'